/// Double sums (limits of rectangular sums) for functions from (Nat, Nat) to Real.
from nat import Nat
from real.real_base import Real
from real.rectangular_sum import rectangular_sum

/// The condition that the rectangular sum of f is within eps of a for all dimensions beyond n.
define double_sum_bounded(f: (Nat, Nat) -> Real, a: Real, n: Nat, eps: Real) -> Bool {
    forall(big_n: Nat) {
        big_n >= n implies rectangular_sum(f, big_n, big_n).is_close(a, eps)
    }
}

/// The double sum of f converges to a.
define double_sum_converges_to(f: (Nat, Nat) -> Real, a: Real) -> Bool {
    forall(eps: Real) {
        eps.is_positive implies exists(n: Nat) {
            double_sum_bounded(f, a, n, eps)
        }
    }
}

/// The double sum of f converges to some value.
define double_sum_converges(f: (Nat, Nat) -> Real) -> Bool {
    exists(a: Real) {
        double_sum_converges_to(f, a)
    }
}

/// The limit of the double sum of f. Only meaningful when f converges.
let double_sum(f: (Nat, Nat) -> Real) -> a: Real satisfy {
    double_sum_converges(f) implies double_sum_converges_to(f, a)
}
