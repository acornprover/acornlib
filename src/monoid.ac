from semigroup import Semigroup
from one import One

/// A multiplicative monoid is a multiplicative semigroup with an identity element.
/// Note that you need the identity to work on the right side as well as the left.
/// One doesn't imply the other.
typeclass M: Monoid extends Semigroup, One {
    /// The identity element must satisfy the identity property on the right.
    mul_identity_right(a: M) {
        a * M.1 = a
    }

    /// The identity element must satisfy the identity property on the left.
    mul_identity_left(a: M) {
        M.1 * a = a
    }
}

from nat.nat_base import Nat

attributes M: Monoid {
    /// Raises a monoid element to a natural number power using repeated multiplication.
    define pow(self, exp: Nat) -> M {
        match exp {
            Nat.0 {
                M.1
            }
            Nat.suc(n) {
                self * self.pow(n)
            }
        }
    }
}

// Proof that a^1 = a
theorem pow_one[M: Monoid](a: M) {
    a.pow(Nat.1) = a
} by {
     // by definition of Nat.1
     // by definition of pow
     // by definition of pow (base case)
     // by mul_identity_right
     // transitivity
}

theorem pow_zero[M: Monoid](a: M) {
    a.pow(Nat.0) = M.1
}

// Proof that a^n * a^m = a^(n+m)
theorem pow_add[M: Monoid](a: M, n: Nat, m: Nat) {
    a.pow(n) * a.pow(m) = a.pow(n + m)
} by {
    // Define a helper function for induction
    define f(x: Nat) -> Bool { pow_add(a, x, m) }

    // Base case: a^0 * a^m = a^(0+m)
    f(Nat.0)

    // Inductive step
    forall(x: Nat) {
        if f(x) {

            // Now prove for x.suc
            a.pow(x.suc) * a.pow(m) = a.pow((x + m).suc)
            f(x.suc)
        }
    }

}

// Proof that (a^n)^m = a ^ (n * m)
theorem pow_pow[M: Monoid](a: M, n: Nat, m: Nat) {
    a.pow(n).pow(m) = a.pow(n * m)
} by {
    // Define a helper function for induction
    define f(x: Nat) -> Bool { pow_pow(a, n, x) }

    // Base case
    f(Nat.0)

    // Inductive step
    forall(x: Nat) {
        if f(x) {
            a.pow(n).pow(x.suc) = a.pow(n * x + n)
            f(x.suc)
        }
    }
}

// Proof that 1^n = 1
theorem one_pow[M: Monoid](n: Nat) {
    M.1.pow(n) = M.1
}